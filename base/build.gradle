apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

static def rebasePath(String... paths) {
    def result = ""
    paths.eachWithIndex { path, index ->
        if (index < paths.length - 1) {
            result += path + File.separator
        } else {
            result += path
        }
    }
    return result
}

String findOrException(name) {
    def result
    if (project.hasProperty(name)) {
        result = project[name]
    } else {
        throw new Exception("cannot find " + name + " in local.properties")
    }
    return result
}
def chromium_dir = findOrException("chromium_dir")

def baseSrc = "base/android/java/src"
def mediaBaseSrc = "media/base/android/java/src"
def netSrc = "net/android/java/src"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"


    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java {
                srcDirs "src/main/java"
                srcDirs "src/main/generated"
                srcDirs "$chromium_dir/$baseSrc",
                        "$chromium_dir/$mediaBaseSrc"
                        "$chromium_dir/$netSrc"

                exclude "**/test/**"
            }
        }

        test {
            java {
                srcDirs "$chromium_dir/$mediaBaseSrc/test"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "javax.annotation:jsr250-api:1.0"
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    def supportLibVersion = '28.0.0'
    implementation "com.android.support:support-v4:${supportLibVersion}"
    implementation "com.android.support:support-compat:${supportLibVersion}"
    implementation "com.android.support:support-core-ui:${supportLibVersion}"
    implementation "com.android.support:support-core-utils:${supportLibVersion}"
    implementation "com.android.support:support-fragment:${supportLibVersion}"

    implementation "androidx.annotation:annotation:1.1.0"

    implementation project(path: ":jniprocessor")
    annotationProcessor project(path: ":jniprocessor")
}

gradle.taskGraph.afterTask { task ->
    if (task.project.name == project.name) {
        if (task.name.matches("compile.*Aidl")) {
            def base = "$chromium_dir/$baseSrc"
            compileAidl(
                    [],
                    [base],
                    ["$base/org/chromium/base/process_launcher/IChildProcessService.aidl",
                     "$base/org/chromium/base/process_launcher/IParentProcess.aidl"]
            )
        }
    }
}


